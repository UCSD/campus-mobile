// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.google.gms:google-services:4.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

ext {
    buildToolsVersion = "27.0.3"
    minSdkVersion = 21
    compileSdkVersion = 27
    targetSdkVersion = 27
    supportLibVersion = "27.1.1"

    gps_version = '15.0.1'
    maps_version = '15.0.1'
    analytics_version = '16.0.1'
    tagmanager_version = '16.0.1'
    measurement_version = '16.0.0'
    invite_version = '16.0.1'
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support' && !details.requested.name.contains('multidex')) {
                details.useVersion "$supportLibVersion"
            }

            if (details.requested.group == 'com.google.android.gms' && !details.requested.name.contains('multidex')) {
                if (details.requested.name.contains('play-services-analytics')) details.useVersion "$analytics_version"
                else if (details.requested.name.contains('play-services-appinvite')) details.useVersion "$invite_version"
                else if (details.requested.name.equals('play-services-tagmanager-v4-impl')) details.useVersion "$tagmanager_version"
                else if (details.requested.name.equals('play-services-measurement-base')) details.useVersion "$measurement_version"
                else {
                    details.useVersion "$gps_version"
                }
            }
        }
    }
}
